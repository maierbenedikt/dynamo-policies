### Target sites
On site.name in [T0_CH_CERN_Disk T1_*_Disk T2_*]

### Deletion trigger
When site.occupancy > 0.9
When site.status == MORGUE
Until site.status != MORGUE and site.occupancy < 0.85

### Replica protection / deletion policies
ProtectBlock blockreplica.is_locked

Delete dataset.status == DEPRECATED and dataset.last_update older_than 1 week ago
Delete dataset.status == INVALID and dataset.last_update older_than 1 week ago

Protect replica.incomplete
Protect dataset.on_tape == PARTIAL
Protect replica.is_last_transfer_source

Protect dataset.name == /*/*/RAW and dataset.on_tape != FULL

Protect dataset.name == /*/*/RECO and dataset.num_full_disk_copy == 0 and dataset.last_update newer_than 90 days ago

ProtectBlock dataset.on_tape == NONE and blockreplica.owner == DataOps

DeleteBlock blockreplica.owner == DataOps

DismissBlock dataset.name == /*/*/RAW and dataset.name != /*Scouting*/*/RAW and blockreplica.last_update older_than 60 days ago
DismissBlock dataset.name == /*/*/RECO and dataset.name != /*/Run*-*-*-v*/RECO and blockreplica.last_update older_than 90 days ago
DismissBlock dataset.name == /*/*LogError*/RAW-RECO and blockreplica.last_update older_than 90 days ago

Protect dataset.on_protected_site and replica.num_full_disk_copy_common_owner < 2

Dismiss dataset.usage_rank > 100
Dismiss dataset.last_update older_than 150 days ago and replica.last_block_created older_than 30 days ago and replica.num_access == 0

Protect dataset.on_tape != FULL and replica.num_full_disk_copy_common_owner < 3
Protect dataset.name == /*/*/MINIAOD* and replica.num_full_disk_copy_common_owner < 3
Protect dataset.name == /*/*/MINIAOD* and replica.last_copy_usa
Protect replica.num_full_disk_copy_common_owner < 2

# Default decision
Dismiss

### Candidate ordering
Order decreasing dataset.usage_rank increasing replica.size
